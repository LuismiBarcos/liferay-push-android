apply plugin: 'com.android.library'
apply plugin: 'jacoco'

apply from: 'https://raw.githubusercontent.com/brunofarache/liferay-mobile-sdk/master/release-aar.gradle'
apply from: 'https://raw.githubusercontent.com/brunofarache/liferay-mobile-sdk/master/formatter.gradle'
format.workingDir = 'src'

dependencies {
	compile group: 'com.google.android.gms', name: 'play-services-base', version: '6.5.87'
	compile group: 'com.liferay.mobile', name: 'liferay-android-sdk', version: '6.2.0.15'
	compile group: 'com.squareup', name: 'otto', version: '1.3.6'

	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.apache.maven', name: 'maven-ant-tasks', version: '2.1.3'
	testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
	testCompile group: 'org.robolectric', name: 'robolectric', version: '2.4'
}

buildscript {
	repositories {
		mavenCentral()
	}

	dependencies {
		classpath 'com.android.tools.build:gradle:1.1.3'
		classpath 'com.github.dcendents:android-maven-plugin:1.2'
	}
}

android {
	compileSdkVersion 21
	buildToolsVersion "21.1.2"

	defaultConfig {
		minSdkVersion 15
		targetSdkVersion 21
	}

	packagingOptions {
		exclude 'META-INF/LICENSE'
		exclude 'META-INF/NOTICE'
	}

	sourceSets {
		main {
			java.srcDirs += 'src/gen/java'
		}

		test {
			java.srcDirs += 'build/generated/source/r/debug'
		}
	}
}

task coverage(type:JacocoReport, dependsOn: "testDebug") {
	def sourceDirs = ['src/gen/java', 'src/main/java']
	description = "Generate test coverage report"

	classDirectories = fileTree(
		dir: "$buildDir/intermediates/classes/debug",
		excludes: [
			'**/R.class',
			'**/R$*.class',
			'**/BuildConfig.*',
			'**/Manifest*.*',
			'**/BitmapRequestBuilder.class',
			'**/DrawableRequestBuilder.class',
			'**/GifRequestBuilder.class'
		]
	)

	additionalSourceDirs = files(sourceDirs)
	sourceDirectories = files(sourceDirs)
	executionData = files("$buildDir/jacoco/testDebug.exec")

	reports {
		html.enabled = true
		xml.enabled = true
	}
}